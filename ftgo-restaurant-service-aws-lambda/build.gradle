apply plugin: IntegrationTestsPlugin
apply plugin: 'docker-compose'

dependencies {
    compile "com.amazonaws:aws-lambda-java-core:1.1.0"
    compile "com.amazonaws:aws-lambda-java-events:2.0.1"

    compile ("io.eventuate.tram.core:eventuate-tram-spring-producer-jdbc") {
        exclude module: "org.springframework"
    }
    compile "io.eventuate.tram.core:eventuate-tram-spring-events"
    compile "io.eventuate.tram.core:eventuate-tram-spring-messaging"
    compile project(":ftgo-restaurant-service-api")
    compile project(":ftgo-common-jpa")
    compile project(":ftgo-common")

    compile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

    compile 'javax.el:javax.el-api:2.2.5'

    testCompile "io.eventuate.util:eventuate-util-test"
    testCompile "io.eventuate.tram.core:eventuate-tram-test-util"

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile 'com.jayway.restassured:rest-assured:2.3.0'
    testCompile "com.jayway.jsonpath:json-path:2.3.0"

    testCompile(platform("io.eventuate.platform:eventuate-platform-dependencies:$eventuatePlatformVersion"))
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_CDC_VERSION", eventuateCdcImageVersion
    environment.put "EVENTUATE_SAGA_VERSION", eventuateTramSagasImageVersion
    environment.put "EVENTUATE_JAVA_BASE_IMAGE_VERSION", eventuateExamplesBaseImageVersion
    environment.put "EVENTUATE_MESSAGING_KAFKA_IMAGE_VERSION", eventuateMessagingKafkaImageVersion

    projectName = null

    integrationTests {
        projectName = null
        removeOrphans = true
        retainContainersOnStartupFailure = true

        startedServices = ['mysql']
        stopContainers = true
    }
}

integrationTest.dependsOn(integrationTestsComposeUp)

build.dependsOn buildZip

